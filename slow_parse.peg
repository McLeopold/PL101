/*
Slow parse

expression:
  (1+2*3)*(4+5*6)

output:
  {
     "expr": 238,
     "matches": {
        "integer": 48,
        "primary": 54,
        "multiplicative": 34,
        "additive": 13,
        "start": 1
     },
     "total": 150
  }
*/

{
  var matches = {
    integer: 0,
    primary: 0,
    multiplicative: 0,
    additive: 0,
    start: 0
  };
}

start
  = expr:additive
    {
      matches['start']++;
      return {
        expr:expr,
        matches:matches,
        total: matches['integer'] +
               matches['primary'] +
               matches['multiplicative'] +
               matches['additive'] +
               matches['start']
      };
    }

additive
  = left:multiplicative "+" right:additive
    { matches['additive']++; return left + right; }
  / m:multiplicative
    { matches['additive']++; return m; }

multiplicative
  = left:primary "*" right:multiplicative
    { matches['multiplicative']++; return left * right; }
  / p:primary
    { matches['multiplicative']++; return p; }

primary
  = i:integer
    { matches['primary']++; return i; }
  / "(" additive:additive ")"
    { matches['primary']++; return additive; }

integer "integer"
  = digits:[0-9]+
    { matches['integer']++; return parseInt(digits.join(""), 10); }
