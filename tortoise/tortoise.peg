/* Grammar for Tortoise, a turtle-graphics language */
{
    var matches = {
        start: 0,
        number_frac: 0,
        number: 0,
        string: 0,
        array: 0,
        validfirstchar: 0,
        validchar: 0,
        identifier: 0,
        statement: 0,
        index: 0,
        statements: 0,
        expression: 0,
        or_op: 0,
        logic_or: 0,
        and_op: 0,
        logic_and: 0,
        comp_op: 0,
        comparative: 0,
        additive_op: 0,
        additive: 0,
        mult_op: 0,
        multiplicative: 0,
        primary: 0,
        comma_expression: 0,
        arglist: 0,
        comma_identifier: 0,
        ident_list: 0,
        ws: 0
    };
}
start
    = ws seq:statements
        { return seq;
          matches['start']++;
          for (var prop in matches) {
            if (!matches[prop]) {
              delete matches[prop];
            }
          }
          return {matches: matches, seq: seq}; }

number_frac
    = "." chars:[0-9]*
        { matches['number_frac']++; return "." + chars.join(''); }

number
    = chars:[0-9]+ frac:number_frac?
        { matches['number']++; return parseFloat(chars.join('') + frac); }
    / "-" chars:[0-9]+ frac:number_frac?
        { matches['number']++; return -parseFloat(chars.join('') + frac); }

string
    = '"' chars:[^"]* '"'
      { matches['string']++; return chars.join(''); }
    / "'" chars:[^']* "'"
      { matches['string']++; return chars.join(''); }

array
    = '[' ws values:arglist ws ']'
      { matches['array']++; return values; }

validfirstchar
    = [a-zA-Z_]

validchar
    = [0-9a-zA-Z_]

identifier
    = firstchar:validfirstchar chars:validchar*
        { matches['identifier']++; return firstchar + chars.join(''); }

statement
    = "if" ws "(" ws expr:expression ws ")" ws 
        "{" ws body:statements ws "}" ws
        "else" ws "{" ws elsebody:statements ws "}" ws
        { matches['statement']++; return { tag:"if", expr:expr, body:body, else:elsebody, loc: [line, column] }; }
    / "if" ws "(" ws expr:expression ws ")" ws 
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"if", expr:expr, body:body, loc: [line, column] }; }
    / "define " ws v:identifier ws "(" ws ")" ws 
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"define", name:v, args:[], body:body, loc: [line, column] }; }
    / "define " ws v:identifier ws "(" ws args:ident_list ws ")"
        ws "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"define", name:v, args:args, body:body, loc: [line, column] }; }
    / "repeat" ws "(" ws expr:expression ws "," ws v:identifier ws ")" ws
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"repeat", expr:expr, name:v, body:body, loc: [line, column] }; }
    / "repeat" ws "(" ws expr:expression ws ")" ws
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"repeat", expr:expr, body:body, loc: [line, column] }; }
    / "repeat" ws
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"repeat", body:body, loc: [line, column] }; }
    / "var " ws v:identifier ws ";" ws
        { matches['statement']++; return { tag:"var", name:v, loc: [line, column] }; }
    / "throw" ws expr:expression? ws ";" ws
        { matches['statement']++; return { tag:"throw", expr: expr || 'Exception!', loc: [line, column] }; }
    / "break" ws ";" ws
        { matches['statement']++; return { tag:"break", loc: [line, column] }; }
    / "continue" ws ";" ws
        { matches['statement']++; return { tag:"continue", loc: [line, column] }; }
    / "return" ws expr:expression ws ";" ws
        { matches['statement']++; return { tag:"return", expr: expr, loc: [line, column] }; }
    / "try" ws
        "{" ws body:statements ws "}" ws
        "catch" ws "{" ws catchbody:statements ws "}" ws
        { matches['statement']++; return { tag:"try", body:body, catch:catchbody, loc: [line, column] }; }
    / "try" ws
        "{" ws body:statements ws "}" ws
        { matches['statement']++; return { tag:"try", body:body, loc: [line, column] }; }
    / "hatch" ws "{" ws body:statements ws "}" ws
        { return {tag:"hatch", body:body, loc: [line, column] }; }
    / v:identifier ws ":=" ws expr:expression ws ";" ws
        { matches['statement']++; return { tag:":=", left:v, right:expr, loc: [line, column] }; }
    / v:identifier is:index+ ws ":=" ws expr:expression ws ";" ws
        { matches['statement']++; return { tag:"[]=", left:v, index:is, right:expr, loc: [line, column] }; }
    / expr:expression ws ";" ws
        { matches['statement']++; return { tag:"ignore", body:expr, loc: [line, column] }; }

index
  = '[' ws index:expression ws ']' ws
    { matches['index']++; return index; }

statements
    = statement*

expression
    = expr:logic_or
        { matches['expression']++; return expr; }

or_op = "||"

logic_or
    = e:logic_and !(ws op:or_op ws)
        { matches['logic_or']++; return e }
    / left:logic_and ws op:or_op ws right:logic_or
        { matches['logic_or']++; return {tag: op, left:left, right:right, loc: [line, column] }; }

and_op = "&&"

logic_and
    = e:comparative !(ws op:and_op ws)
        { matches['logic_and']++; return e }
    / left:comparative ws op:and_op ws right:logic_and
        { matches['logic_and']++; return {tag: op, left:left, right:right, loc: [line, column] }; }

comp_op = "<=" / ">=" / "!=" / "==" / "<" / ">"

comparative
    = e:additive !(ws op:comp_op ws)
        { matches['comparative']++; return e }
    / left:additive ws op:comp_op ws right:comparative
        { matches['comparative']++; return {tag: op, left:left, right:right, loc: [line, column] }; }

additive_op = "+" / "-"

additive
    = e:multiplicative !(ws op:additive_op ws)
        { matches['additive']++; return e }
    / left:multiplicative ws op:additive_op ws right:additive
        { matches['additive']++; return {tag:op, left:left, right:right, loc: [line, column] }; }

mult_op = "*" / "/"

multiplicative
    = e:primary !(ws op:mult_op ws)
        { matches['multiplicative']++; return e }
    / left:primary ws op:mult_op ws right:multiplicative
        { matches['multiplicative']++; return {tag:op, left:left, right:right, loc: [line, column] }; }

primary
    = number
    / string
    / array
    / v:identifier is:index+
        { matches['primary']++; return {tag:"index", name:v, index:is, loc: [line, column] }; }
    / v:identifier "(" ws ")"
        { matches['primary']++; return {tag:"call", name:v, args:[], loc: [line, column] }; }
    / v:identifier "(" ws args:arglist ws ")"
        { matches['primary']++; return {tag:"call", name:v, args:args, loc: [line, column] }; }
    / v:identifier
        { matches['primary']++; return {tag:"ident", name:v, loc: [line, column] }; }
    / "(" ws expression:expression ws ")"
        { matches['primary']++; return expression; }

comma_expression = "," ws expr:expression
        { matches['comma_expression']++; return expr; }

arglist
    = first:expression rest:comma_expression*
        { matches['arglist']++; return [first].concat(rest); }

comma_identifier = "," ws v:identifier
        { matches['comma_identifier']++; return v; }

ident_list
    = first:identifier rest:comma_identifier*
        { matches['ident_list']++; return [first].concat(rest); }

SourceChar
    = [\u0000-\uffff]
LineChar
    = (!(EOLChar) SourceChar)
EOLChar
    = [\u000a\u000b\u000c\u000d\u0085\u2028\u2029]
SpaceChar
    = [\u0009\u0020\u00a0\u1680\u180e\u2000\u2001\u2002\u2003\u2004\u2005\u2006]
CommentChar
    = '//'
Comment
    = CommentChar LineChar*

ws
    = (SpaceChar / EOLChar / Comment)*
